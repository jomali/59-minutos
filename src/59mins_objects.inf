

!!==============================================================================
!!
!!	59 MINUTOS
!!	Definición de localidades y objetos de la casa
!!
!!==============================================================================
!!
!!	File:			59mins_objects.inf
!!	Author(s):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Languague:		ES (Castellano)
!!	System:			Inform-INFSP 6
!!	Platform:		Máquina-Z / GLULX
!!	Version:		0.1
!!	Released:		2014/03/05
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de 59 MINUTOS.
!!
!!	59 MINUTOS es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General GNU
!!	publicada por la Fundación para el Software Libre, ya sea la versión
!!	3 de la Licencia, o (a su elección) cualquier versión posterior.
!!
!!	59 MINUTOS se distribuye con la esperanza de que sea útil, pero
!!	SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita MERCANTIL o
!!	de APTITUD PARA UN PROPÓSITO DETERMINADO. Consulte los detalles de
!!	la Licencia Pública General GNU para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública General GNU
!!	junto a 59 MINUTOS. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2014, J. Francisco Martín
!!
!!------------------------------------------------------------------------------
!!
!!	# TABLA DE CONTENIDOS:
!!
!!	1)	LIMBO: En algún lugar.
!!	2)	BAÑO: Baño.
!!	3)	COCINA: Cocina.
!!	4)	DORMITORIO: Dormitorio.
!!	5)	HABITACION: Habitación multiusos.
!!	6)	PASILLO: Pasillo de la segunda planta.
!!	7)	PLAYA: Playa.
!!	8)	RECIBIDOR: Recibidor.
!!	9)	SALON: Salón.
!!	10)	TALLER: Pequeño taller.
!!	11)	TERRAZA: Terraza.
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	1)	LIMBO: En algún lugar.
!!------------------------------------------------------------------------------

Room	X00_LIMBO "En algún lugar"
 with	name_m	'babia' 'limbo' 'lugar' 'sueno' 'vacio',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		clarification 0,
 has	male;

Object	-> X00_Silvia "Silvia"
 with	name	'personaje' 'protagonista',
		name_f	'silvia',
		gender	G_FEMENINO,
		adjectives 'principal',
		description [; return O__M(self, 1); ],
 has	animate female proper;

Object	-> -> X00_botas "botas"
 with	name_f	'bota',
		name_fp	'botas',
		name_m	'calzado',
		gender	G_FEMENINO + G_PLURAL,
		article "sus",
		description [; return O__M(self, 1); ],
 has	clothing female pluralname worn;

Object	-> -> X00_ropa "ropa"
 with	name_f	'ropa',
		gender	G_FEMENINO,
		article "su",
		description [; return O__M(self, 1); ],
 has	clothing female worn;

Atrezzo	X00_paredes "paredes"
 with	found_in [; return (location ofclass Indoors); ],
		name_f	'pared' 'esquina',
		name_fp	'paredes' 'esquinas',
		name_m	'muro',
		name_mp	'muros',
		gender	G_FEMENINO + G_PLURAL,
		before [;
			Examine, Search:
				switch (GRAMMATICAL_INFLECTION) {
					1,2,3:	return O__M(self, 1);
					4,5,6:	return O__M(self, 2);
				}
		],
 has	female pluralname;

Atrezzo	X00_ventanas "ventanas"
 with	found_in [; return (location ofclass Indoors); ],
		name_f	'ventana',
		name_fp	'ventanas',
		name_m	'ventanal' 'ventano',
		name_mp	'ventanales' 'ventanos',
		gender	G_FEMENINO + G_PLURAL,
		before [;
			Examine, Search:
				switch (GRAMMATICAL_INFLECTION) {
					1,2,3:
						switch (real_location) {
							H01_BANNO, H04_HABITACION:
								return O__M(self, 3);
							H02_COCINA:
								return O__M(self, 4);
							H03_DORMITORIO:
								return O__M(self, 5);
							H08_RECIBIDOR:
								return O__M(self, 6);
							H09_SALON:
								return O__M(self, 7);
							H10_TALLER:
								return O__M(self, 8);
							H11_TERRAZA:
								return O__M(self, 9);
							default:
								return O__M(self, 1);
						}
					4,5,6:
						return O__M(self, 2);
				}
		],
 has	female pluralname;


!!==============================================================================
!!	2)	BAÑO: Baño.
!!------------------------------------------------------------------------------

Indoors	H01_BANNO "Baño"
 with	name_m	'aseo' 'bano' 'lavabo' 'servicio',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		s_to	H03_DORMITORIO,
		sw_to	H06_PASILLO,
 has	male;

Furniture	-> H01_armario "armario auxiliar"
 with	name_m	'armario' 'armarito' 'mobiliario' 'mueble',
		name_mp	'muebles',
		gender	G_MASCULINO,
		adjectives 'auxiliar',
		description [; return O__M(self, 1); ],
 has	male;

Atrezzo	-> -> H01_productosHigiene "productos de higiene"
 with	name_m	'producto',
		name_mp	'productos',
		gender	G_MASCULINO + G_PLURAL,
		article "algunos",
		adjectives 'higiene',
		description [; return O__M(self, 1); ],
 has	male pluralname;

Item	-> -> H01_medicinas "medicinas"
 with	name_f	'medicina',
		name_fp	'medicinas',
		name_m	'tranquilizante',
		name_mp	'tranquilizantes',
		gender	G_FEMENINO + G_PLURAL,
		description [; return O__M(self, 1); ],
 has	female pluralname;

Item	-> -> H01_velas "velas aromáticas"
 with	name_f	'vela',
		name_fp	'velas',
		gender	G_FEMENINO + G_PLURAL,
		adjectives 'aromatica' 'aromaticas',
		description [; return O__M(self, 1); ],
 has	female pluralname;

Furniture	-> H01_bannera "bañera"
 with	name_f	'banera',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
 has	female;

Furniture	-> H01_espejo "espejo"
 with	name_m	'espejo' 'reflejo',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	male;


!!==============================================================================
!!	3)	COCINA: Cocina.
!!------------------------------------------------------------------------------

Indoors	H02_COCINA "Cocina"
 with	name_f	'cocina',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
		n_to	H11_TERRAZA,
		e_to	H09_SALON,
 has	female;

!! TODO:
!!	 -	beber café
Item	H02_cafe "taza de café" X00_LIMBO
 with	name_f	'bebida' 'taza',
		name_m	'cafe' 'tazon' 'vaso',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
 has	female;

Furniture	-> H02_encimera
 with	short_name [; print "encimera"; return true; ],
		name_f	'encimera',
		name_m	'aparador' 'mobiliario' 'moblaje' 'mueble',
		name_mp	'muebles',
		gender	G_FEMENINO,
		before [;
			DefaultAction:
				<<Search self>>;
			Examine:
				<<Search self>>;
			LetGo:
				return false;
			Receive:
				return false;
			Search:
				!! 1) Hay objetos sobre la encimera:
				if (child(self)) {
					print "Sobre ", (the) self, " ve ";
					WriteListFrom(child(self), ENGLISH_BIT + PARTINV_BIT);
					".";
				}
				!! 2) NO hay objetos sobre la encimera:
				else {
					"No hay nada sobre ", (the) self, ".";
				}
			default:
				print "No es necesario que se preocupe por ";
				SpanishNominativePronoun(self);
				".";
		],

		after [;
			LetGo:
				give noun ~concealed;
			Receive:
				give noun concealed;
		],
 has	female scenery supporter;

Item	-> -> H02_cafetera "cafetera"
 with	name_f	'cafetera',
		gender	G_FEMENINO,
		adjectives 'goteo',
		before [;
			Examine:
				return O__M(self, 1);
			Pull, Push, PushDir, Take, Turn:
				"La ", (name) self, " ya está bien donde está.";
			SwitchOff:
				"Ya estaba apagada.";
			SwitchOn:
				!! 1) Silvia se prepara un café:
				if (self hasnt general) {
					give self general;
					move H02_cafe to parent(self);
					return O__M(self, 2);
				}
				!! 2) Silvia ya se había preparado el café:
				return O__M(self, 3);
		],
 has	concealed female ~on switchable;

Item	-> -> H02_mechero "mechero"
 with	name_m	'encendedor' 'mechero',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	concealed male;

Furniture	-> H02_mesa "mesa"
 with	name_f	'mesa',
		name_m	'mobiliario' 'mueble',
		name_mp	'muebles',
		gender	G_FEMENINO,
		describe [ i obj;
			!! 1) Retorna cuando no hay ningún objeto sobre la mesa:
			if (child(self) == nothing) return true;
			!! 2) Imprime el listado de objetos que hay sobre la mesa:
			objectloop(obj in self) {
				if (obj ~= player && obj hasnt scenery or concealed)
					i++;
			}
			if (i == 0) return true;
			print "^Sobre ", (the) self, " puede ver ";
			WriteListFrom(child(self), CONCEAL_BIT + ENGLISH_BIT + PARTINV_BIT);
			".";
		],
		description [; return O__M(self, 1); ],
		react_before [;
			Drop:
				<<PutOn noun self>>;
		],
		before [;
			Climb, Enter:
				"En realidad no tiene motivos para subirse ahí.";
		],
		after [ i obj;
			LetGo, Receive:
				!! Tras colocar o quitar objetos de encima de la mesa se 
				!! comprueba si sigue quedando sobre ella algún objeto 
				!! relevante (en este caso consideramos objetos relevantes 
				!! aquellos que no tengan los atributos "scenery" o 
				!! "concealed"). Si no los hay, la mesa debe tener el atributo 
				!! "concealed" para que sea únicamente la descripción de la 
				!! localidad quien se encargue de hacer mención a ella. En 
				!! cambio, si los hay debe dejar de tener el atributo 
				!! "concealed" para que la librería imprima automáticamente el 
				!! mensaje descriptivo que lista los objetos que tiene la cama 
				!! encima.
				objectloop(obj in self) {
					if (obj ~= player && obj hasnt scenery or concealed)
						i++;
				}
				if (i == 0) give self concealed;
				else give self ~concealed;
		],
 has	female supporter;

Item	-> -> H02_periodico "periódico"
 with	name_f	'prensa',
		name_m	'diario' 'periodico',
		gender	G_MASCULINO,
		adjectives 'montanes',
		description [; return O__M(self, 1); ],
		before [;
			Open, Read:
				!! 1) Al abrirlo por primera vez:
				if (self hasnt general) {
					give self general;
					return O__M(self, 2);
				}
				!! 2) Al abrirlo de nuevo:
				return O__M(self, 3);
		],
 has	male;

Furniture	-> H02_nevera "nevera"
 with	name_f	'nevera',
		name_m	'electrodomestico' 'refrigerador',
		name_mp	'electrodomesticos',
		gender	G_FEMENINO,
		adjectives 'aparato',
		description [; return O__M(self, 1); ],
 has	concealed female;


!!==============================================================================
!!	4)	DORMITORIO: Dormitorio.
!!------------------------------------------------------------------------------

Indoors	H03_DORMITORIO "Dormitorio"
 with	name_f	'habitacion',
		name_m	'cuarto' 'dormitorio',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		n_to	H01_BANNO,
		w_to	H06_PASILLO,
 has	male;

Item	-> H03_cajaZapatos "caja de zapatos"
 with	name_f	'caja',
 has	female;

Furniture	-> H03_cajon "cajón"
 with	name_m	'cajon',
 has	male;

Item	-> H03_llave "llave"
 with	name_f	'llave',
 has	female;

Furniture	-> H03_mesitaNoche "mesita de noche"
 with	name_f	'mesita',
 has	female;

Item	-> H03_preservativos "caja de preservativos"
 with	name_f	'caja',
		name_mp	'condones' 'preservativos',
 has	female;

Furniture	-> H03_radiodespertador "radiodespertador"
 with	name_m	'radiodespertador',
 has	male;

Atrezzo	-> H03_trastos "trastos"
 with	name_mp	'trastos',
 has	male pluralname;

Atrezzo	-> H03_ropaInterior "ropa interior"
 with	name_f	'ropa',
 has	female;

Furniture	-> H03_cama "cama"
 with	name_f	'cama' 'piltra',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
 has	female;


!!==============================================================================
!!	5)	HABITACION: Habitación multiusos.
!!------------------------------------------------------------------------------

Indoors	H04_HABITACION "Habitación multiusos"
 with	name_f	'habitacion',
		name_m	'cuarto',
		gender	G_FEMENINO,
		adjectives 'multiusos',
		description [; return O__M(self, 1); ],
		s_to	H06_PASILLO,
 has	female;

Item	-> H04_cajaHerramientas "caja de herramientas"
 with	name_f	'caja',
		name_fp	'herramientas',
 has	female;

Furniture	-> H04_puerta "puerta del armario"
 with	name_f	'puerta',
 has	female;

Furniture	-> H04_silla "silla"
 with	name_f	'silla',
 has	female;

Furniture	-> H04_armario "armario"
 with	name_m	'armario' 'mobiliario' 'mueble',
		name_mp	'muebles',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	male ~open openable;

Furniture	-> H04_bicicletaEstatica "bicicleta estática"
 with	name_f	'bici' 'bicicleta',
		gender	G_FEMENINO,
		adjectives 'estatica',
		description [; return O__M(self, 1); ],
 has	female;

Furniture	-> H04_escritorio "escritorio"
 with	name_f	'mesa',
		name_m	'escritorio' 'mueble' 'mobiliario',
		name_mp	'muebles',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	male;

Item	-> -> H04_bolso "bolso"
 with	name_m	'bolso',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	male;

Item	-> -> H04_cuaderno "cuaderno con motivos de ~El Principito~"
 with	name_f	'libreta',
		name_m	'cuaderno',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	male;

Item	-> -> H04_examenes
 with	short_name [; print "exámenes sin corregir"; ],
		name_m	'examen',
		name_mp	'examenes',
		gender	G_MASCULINO + G_PLURAL,
		adjectives 'corregido' 'corregidos' 'corregir' 'por' 'sin',
		description [; return O__M(self, 1); ],
 has	male pluralname;

Item	-> H04_balonBaloncesto "balón de baloncesto"
 with	name_f	'pelota',
		name_m	'balon',
		gender	G_MASCULINO,
		adjectives 'baloncesto',
		description [; return O__M(self, 1); ],
 has	male;


!!==============================================================================
!!	6)	PASILLO: Pasillo de la segunda planta.
!!------------------------------------------------------------------------------

Indoors	H06_PASILLO "Pasillo de la segunda planta"
 with	name_m	'pasillo',
		gender	G_MASCULINO,
		adjectives 'segunda' 'planta',
		description [; return O__M(self, 1); ],
		n_to	H04_HABITACION,
		e_to	H03_DORMITORIO,
		ne_to	H01_BANNO,
		d_to	H08_RECIBIDOR,
 has	male;

Item	-> H06_fotografias "viejas fotografías"
 with	name_fp	'fotografias' 'fotos',
		gender	G_FEMENINO + G_PLURAL,
		adjectives 'enmarcadas' 'viejas',
		description [; return O__M(self, 1); ],
 has	female pluralname remarkable;

Item	-> H06_fotografia1 "fotografía de su madre de joven"
 with	name_f	'fotografia' 'foto',
		gender	G_FEMENINO,
		adjectives 'joven' 'madre' 'su',
		description [; return O__M(self, 1); ],
 has	female;

Item	-> H06_fotografia2 "fotografía de su noveno cumpleaños"
 with	name_f	'fotografia' 'foto',
		gender	G_FEMENINO,
		adjectives 'anos' 'cumpleanos' 'noveno' 'nueve' 'su',
		description [; return O__M(self, 1); ],
 has	female;

Item	-> H06_fotografia3 "fotografía de carnavales"
 with	name_f	'fotografia' 'foto',
		gender	G_FEMENINO,
		adjectives
			'carnaval' 'carnavales' 'colegio' 'disfraces' 'disfraz' 
			'disfrazada' 'disfrazadas' 'disfrazados',
		description [; return O__M(self, 1); ],
 has	female;

Item	-> H06_fotografia4 "fotografía de ella con su padre"
 with	name_f	'fotografia' 'foto',
		gender	G_FEMENINO,
		adjectives 'con' 'ella' 'nina' 'padre' 'playa' 'su',
		description [; return O__M(self, 1); ],
 has	female;


!!==============================================================================
!!	7)	PLAYA: Playa.
!!------------------------------------------------------------------------------

Outdoors	H07_PLAYA "Playa"
 with	name_f	'playa',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
		s_to	H11_TERRAZA,
 has	female;

Atrezzo	-> H07_casa "casa"
 with	name_f	'casa',
		name_m	'edificio',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
 has	female;

Atrezzo	-> H07_mar "mar"
 with	name_f	'mar',
		name_m	'atlantico' 'cantabrico' 'oceano',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
 has	female;


!!==============================================================================
!!	8)	RECIBIDOR: Recibidor.
!!------------------------------------------------------------------------------

Indoors	H08_RECIBIDOR "Recibidor"
 with	name_m	'recibidor',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		n_to	H09_SALON,
		u_to	H06_PASILLO,
 has	male;

Furniture	-> H08_comoda "cómoda"
 with	real_name [; print "cómoda"; ],
		name_f	'comoda',
		name_m	'mobiliario' 'moblaje' 'mueble',
		name_mp	'muebles',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
		before [;
			Climb, Enter, Sit:
				"En realidad no tiene motivos para subirse ahí.";
			Close:
				<<Close H08_cajon>>;
			Open:
				<<Open H08_cajon>>;
		],
 has	concealed female supporter;

Item	-> -> H08_telefono "teléfono"
 with	real_name [; print "teléfono"; ],
		name_m	'auricular' 'telefono' 'telf' 'tlf',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		before [;
			Pull, Push, PushDir, Take, Turn:
				print_ret (The) self, " ya está", (n) self, 
				" donde está", (n) self, ".";
		],
 has	male remarkable;

Furniture	-> H08_cajon "cajón"
 with	name_f	'gaveta',
		name_m	'cajon' 'cajoncillo' 'compartimento',
		gender	G_MASCULINO,
		adjectives 'comoda',
		before [;
			DefaultAction, Examine:
				<<Search self>>;
			Pull:
				<<Open self>>;
			Push:
				<<Close self>>;
			PushDir, Take, Turn:
				"Forma parte ", (del_) H08_comoda, ".";
		],
 has	concealed container male ~open openable;

Item	-> -> H08_cajetilla "cajetilla de tabaco"
 with	name_f	'cajetilla',
		name_m	'paquete' 'tabaco',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
 has	female ~open openable remarkable;


!!==============================================================================
!!	9)	SALON: Salón.
!!------------------------------------------------------------------------------

Indoors	H09_SALON "Salón"
 with	name_f	'sala',
		name_m	'comedor' 'salon',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		s_to	H08_RECIBIDOR,
		w_to	H02_COCINA,
 has	male;

Atrezzo	"estanterías" H09_SALON
 with	name_f	'balda' 'estanteria' 'repisa',
		name_fp	'baldas' 'estanterias' 'repisas',
		name_m	'anaquel' 'estante' 'pluteo',
		name_mp	'anaqueles' 'estantes' 'pluteos',
		gender	G_FEMENINO + G_PLURAL,
		before [;
			DefaultAction, Examine, Search:
				<<Examine H09_libros>>;
			default:
				print "No es necesario preocuparse por ";
				SpanishNominativePronoun(self);
				".";
		],
 has	female pluralname;

Furniture	-> H09_DVD "reproductor de DVD"
 with	name_m	'reproductor' 'dvd',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		before [;
			DefaultAction, Examine:
				!! 1) Cuando se ha puesto la película en el DVD:
				if (self has general) return O__M(self, 5);
				!! 2) La tele está apagada:
				if (self hasnt on) return O__M(self, 1);
				!! 3) Está encendida:
				return O__M(self, 2);
			Pull, Push, PushDir, Take, Turn:
				"El ", (name) self, " ya está bien donde está.";
		],
		after [;
			SwitchOn:
				!! 1) Se enciende el DVD con la tele apagada:
				if (H09_televisor hasnt on) return O__M(self, 3);
				!! 2) Se enciende el DVD con la tele encendida. Al intentarlo 
				!! por primera vez:
				if (self hasnt general) {
					give self general;
					return O__M(self, 4);
				}
				!! 3) Se enciende el DVD con la tele encendida. Respuesta por 
				!! defecto:
				return O__M(self, 4); !! TODO - personalizar mensaje
		],
 has	concealed male ~on switchable;

Atrezzo	-> H09_libros "libros"
 with	name_m	'libro',
		name_mp	'libros',
		gender	G_MASCULINO + G_PLURAL,
		description [;
			!! 1) Al examinar los libros por primera vez:
			if (self hasnt described) {
				move H09_tiempoPerdido to parent(self);
				return O__M(self, 1);
			}
			!! 2) Al examinar los libros de nuevo:
			return O__M(self, 2);
		],
		before [;
			DefaultAction, Examine, Search:
				return false;
			default:
				print "No es necesario que se preocupe por ";
				SpanishNominativePronoun(self);
				".";
		],
 has	male pluralname;

Furniture	-> H09_muebleBar "mueble bar"
 with	name_m	'bar' 'mueble',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		before [;
			Pull, Push, PushDir, Take, Turn:
				"El ", (name) self, " está fijado a la pared.";
		],
 has	concealed container male ~open openable;

Item	-> -> H09_botellaGuisqui "botella de güisqui"
 with	name_f	'botella',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
 has	female;

Atrezzo	-> -> H09_trofeos "trofeos"
 with	name_m	'premio' 'trofeo',
		name_mp	'premios' 'trofeos',
		gender	G_MASCULINO + G_PLURAL,
		description [; return O__M(self, 1); ],
 has	male pluralname ~scenery;

Furniture	-> H09_televisor "televisor"
 with	name_f	'tele' 'tv',
		name_m	'televisor',
		gender	G_MASCULINO,
		before [;
			DefaultAction, Examine:
				!! 1) Cuando se ha puesto la película en el DVD:
				if (H09_DVD has general) return O__M(H09_DVD, 5);
				!! 2) La tele está apagada:
				if (self hasnt on) return O__M(self, 1);
				!! 3) Está encendida:
				return O__M(self, 2);
			Pull, Push, PushDir, Take, Turn:
				"El ", (name) self, " ya está bien donde está.";
		],
		after [;
			SwitchOn:
				!! 1) Se enciende la tele con el DVD apagado:
				if (H09_DVD hasnt on) return O__M(self, 3);
				!! 2) Se enciende la tele con el DVD encendido. Al intentarlo 
				!! por primera vez:
				if (H09_DVD hasnt general) {
					give H09_DVD general;
					return O__M(H09_DVD, 4);
				}
				!! 3) Se enciende la tele con el DVD encendido. Respuesta por 
				!! defecto:
				return O__M(H09_DVD, 4); !! TODO - personalizar mensaje
		],
 has	concealed male ~on switchable;

Item	H09_tiempoPerdido "en busca del tiempo perdido" X00_LIMBO
 with	name_m	'libro' 'tiempo' 'perdido',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		before [;
			Open, Read:
				!! 1) Al leer el libro por primera vez:
				if (self hasnt general) {
					give self general;
					return O__M(self, 2);
				}
				!! 2) Al leer el libro de nuevo:
				return O__M(self, 3);
		],
 has	male;


!!==============================================================================
!!	10)	TALLER: Pequeño taller.
!!------------------------------------------------------------------------------

Indoors	H10_TALLER "Pequeño taller"
 with	name_m	'taller',
		gender	G_MASCULINO,
		adjectives 'pequeno',
		description [; return O__M(self, 1); ],
		e_to	H11_TERRAZA,
		out_to	[; return self.e_to(); ],
 has	male;

Object	-> H10_arcon "arcón"
 with	name_m	'arcon',
 has	male;

Object	-> H10_cerradura "cerradura"
 with	name_f	'cerradura',
 has	female;

Object	-> H10_mesa "mesa de trabajo"
 with	name_f	'mesa'
 has	female;

Object	-> H10_dibujo "dibujo enmarcado"
 with	name_m	'dibujo',
		gender	G_MASCULINO,
		adjectives 'enmarcado',
		description [; return O__M(self, 1); ],
 has	male;

Object	-> H10_disolventes "disolventes"
 with	name_m	'disolvente',
		name_mp	'disolventes',
		gender	G_MASCULINO + G_PLURAL,
		before [; return H10_pinturas.before(); ],
 has	male pluralname;

Object	-> H10_lienzo "lienzo inacabado"
 with	name_m	'lienzo',
		gender	G_MASCULINO,
		adjectives 'inacabado',
		description [; return O__M(self, 1); ],
 has	male;

Object	-> H10_lienzos "lienzos"
 with	name_mp	'lienzos',
		gender	G_MASCULINO + G_PLURAL,
		description [; return O__M(self, 1); ],
 has	male pluralname;

Object	-> H10_pinceles "pinceles"
 with	name_m	'pincel',
		name_mp	'pinceles',
		gender	G_MASCULINO + G_PLURAL,
		before [; return H10_pinturas.before(); ],
 has	male pluralname;

Object	-> H10_pinturas "pinturas"
 with	name_f	'pintura',
		name_fp	'pinturas',
		gender	G_FEMENINO + G_PLURAL,
		description [; return O__M(self, 1); ],
 has	female pluralname;

Object	-> H10_rifle "rifle de caza"
 with	name_f	'arma',
		name_m	'rifle',
		gender	G_MASCULINO,
		adjectives 'caza',
		description [; return O__M(self, 1); ],
 has	male;


!!==============================================================================
!!	11)	TERRAZA: Terraza.
!!------------------------------------------------------------------------------

Outdoors	H11_TERRAZA "Terraza"
 with	name_f	'terraza',
		name_m	'patio',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
		n_to	H07_PLAYA,
		s_to	H02_COCINA,
		w_to	H10_TALLER,
 has	female;

Atrezzo	-> H11_macetas "macetas"
 with	name_fp	'macetas',
 has	female pluralname;

Atrezzo	-> H11_muebles "muebles"
 with	name_mp	'muebles',
 has	male pluralname;

Furniture	-> H11_aroBaloncesto "aro de baloncesto"
 with	name_f	'canasta',
		name_m	'aro',
		gender	G_MASCULINO,
		adjectives 'baloncesto',
		description [; return O__M(self, 1); ],
 has	male;

Furniture	-> H11_caballete "caballete"
 with	name_m	'caballete',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	male ~open openable;


