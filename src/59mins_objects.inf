

!!==============================================================================
!!
!!	59 MINUTOS
!!	Definición de localidades y objetos de la casa
!!
!!==============================================================================
!!
!!	File:			59mins_objects.inf
!!	Author(s):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Languague:		ES (Castellano)
!!	System:			Inform-INFSP 6
!!	Platform:		Máquina-Z / GLULX
!!	Version:		0.1
!!	Released:		2014/03/05
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de 59 MINUTOS.
!!
!!	59 MINUTOS es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General GNU
!!	publicada por la Fundación para el Software Libre, ya sea la versión
!!	3 de la Licencia, o (a su elección) cualquier versión posterior.
!!
!!	59 MINUTOS se distribuye con la esperanza de que sea útil, pero
!!	SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita MERCANTIL o
!!	de APTITUD PARA UN PROPÓSITO DETERMINADO. Consulte los detalles de
!!	la Licencia Pública General GNU para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública General GNU
!!	junto a 59 MINUTOS. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2014, J. Francisco Martín
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	LIMBO: En algún lugar
!!------------------------------------------------------------------------------

Room	obj_LIMBO "En algún lugar"
 with	name_m	'limbo' 'lugar' 'sueno' 'vacio',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		clarification 0,
 has	male;

!!	 *	FIXME - adjectives
Atrezzo	"paredes" 
 with	found_in
			obj_BANNO obj_COCINA obj_DORMITORIO obj_HABITACION obj_PASILLO 
			obj_RECIBIDOR obj_SALON obj_TALLER obj_TERRAZA,
		name_m	'muro',
		name_mp	'muros',
		name_f	'pared' 'esquina',
		name_fp	'paredes' 'esquinas',
		gender	G_FEMENINO + G_PLURAL,
		adjectives
			'amarillenta' 'amarillentas' 'amarillento' 'amarillentos' 'blanca' 
			'blancas' 'blanco' 'blancos',
		before [;
			Examine, Search:
				switch (GRAMMATICAL_INFLECTION) {
					1,2,3: "No hay nada especial en ", (the) self, ".";
					4,5,6: "No había nada especial en ", (the) self, ".";
				}
			default:
				switch (GRAMMATICAL_INFLECTION) {
					1: "No creo que sea necesario preocuparme por ";
					2: "No es necesario que te preocupes por ";
					3: "No es necesario que se preocupe por ";
					4: "No creí que fuese necesario preocuparme por ";
					5: "No era necesario que te preocupases por ";
					6: "No era necesario que se preocupase por ";
				}
				SpanishNominativePronoun(self);
				".";
		], 
 has	female pluralname;

Object	obj_Silvia "Silvia" obj_LIMBO
 with	name_f	'silvia',
		name_m	'personaje',
		gender	G_FEMENINO,
		description [; return L__M(##Miscellany, 19); ],
 has	animate female proper;

Item	obj_ropa "ropa" obj_Silvia
 class	Receptacle
 with	name_f	'ropa',
		gender	G_FEMENINO,
		article	 "su",
		adjectives 'brown' 'olive' 'green',
		description [; return O__M(self, 1); ],
		react_before [;
			DisrobeX:
				if (noun == nothing)
					<<Disrobe self>>;
			WearX:
				if (noun == nothing)
					<<Wear self>>;
		],
		before [;
		],
		after [;
		],
		capacity_size 3,
		size 4,
 has	clothing container female open worn;


!!==============================================================================
!!	COCINA
!!------------------------------------------------------------------------------

Room	obj_COCINA "Cocina"
 with	name_f	'cocina',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
		e_to	obj_SALON,
		n_to	obj_TERRAZA,
 has	female;


!!==============================================================================
!!	TERRAZA
!!------------------------------------------------------------------------------

Room	obj_TERRAZA "Terraza"
 with	name_f	'terraza',
		name_m	'patio',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
		n_to	obj_PLAYA,
		s_to	obj_COCINA,
		w_to	obj_TALLER,
 has	female;

Atrezzo	obj_aro "aro de baloncesto" obj_TERRAZA
 with	name_f	'canasta',
		name_m	'aro',
		gender	G_MASCULINO,
		adjectives 'baloncesto',
		before [;
			DefaultAction:
				if (self has described)
					<<Use self>>;
			Use:
				if (~~IndirectlyContains(player, obj_pelotaBaloncesto))
					"No puede jugar sin un balón.";
				switch (self.counter) {
					0:
						print "Se coloca justo bajo el aro y se prepara para 
						lanzar...^";
					1:
						print "Se coloca a un paso del aro y se prepara para 
						lanzar...^";
					7:
						print "Nunca ha sido capaz de meterla desde tan lejos. 
						Se prepara para tirar desde ", (number) self.counter, 
						" pasos de distancia del aro...^";
					default:
						print "Se coloca a ", (number) self.counter, " pasos 
						de la canasta y se prepara para lanzar un tiro 
						libre...^";
				}
				print "Vel: ", self.speed;
				press_any_key();
				switch (self.free_throw()) {
					0:
						if (self.speed > 2) {
							self.speed = self.speed - 2;
							self.counter++;
						}
						"^^¡Encesta!";
					1:
						print "^^La pelota da en el aro y sale rebotada sin 
						entrar.^";
					default:
						print "^^Falla el tiro.^";
				}
				self.speed = self.start_speed;
				self.counter = 0;
				return true;
		],
 private
		free_throw [ x;
			AimingManager.set_timer_frequency(self.speed);
			x = AimAndFire();
			if (x == -2 || x == -1) return random(0,1,2,3,4);
			if (x < 3) return 0;
			if (x < 8) return 1;
			return 2;
		],
		counter 0,
		speed 20,
		start_speed 20,
 has	male;


!!==============================================================================
!!	TALLER
!!------------------------------------------------------------------------------

Room	obj_TALLER "Pequeño taller"
 with	name_m	'taller',
		gender	G_MASCULINO,
		adjectives 'pequeno',
		description [; return O__M(self, 1); ],
		e_to	obj_TERRAZA,
 has	male;


!!==============================================================================
!!	PLAYA
!!------------------------------------------------------------------------------

Room	obj_PLAYA "Playa"
 with	name_f	'playa',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
		s_to	obj_TERRAZA,
 has	female;


!!==============================================================================
!!	SALON
!!------------------------------------------------------------------------------

Room	obj_SALON "Salón"
 with	name_f	'sala',
		name_m	'salon' 'comedor',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		s_to	obj_RECIBIDOR,
		w_to	obj_COCINA,
 has	male;


!!==============================================================================
!!	RECIBIDOR
!!------------------------------------------------------------------------------

Room	obj_RECIBIDOR "Recibidor"
 with	name_m	'recibidor',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		n_to	obj_SALON,
		u_to	obj_PASILLO,
 has	male;


!!==============================================================================
!!	PASILLO
!!------------------------------------------------------------------------------

Room	obj_PASILLO "Pasillo de la segunda planta"
 with	name_m	'pasillo',
		gender	G_MASCULINO,
		adjectives 'segunda' 'planta',
		description [; return O__M(self, 1); ],
		d_to	obj_RECIBIDOR,
		e_to	obj_DORMITORIO,
		n_to	obj_HABITACION,
		ne_to	obj_BANNO,
 has	male;


!!==============================================================================
!!	HABITACION
!!------------------------------------------------------------------------------

Room	obj_HABITACION "Habitación multiusos"
 with	name_f	'habitacion',
		gender	G_FEMENINO,
		adjectives 'multiusos',
		description [; return O__M(self, 1); ],
		s_to	obj_PASILLO,
 has	female;

Item	obj_pelotaBaloncesto "pelota de baloncesto" obj_HABITACION
 with	name_f	'pelota',
		name_m	'balon',
		gender	G_FEMENINO,
		adjectives 'baloncesto',
 has	female;


!!==============================================================================
!!	DORMITORIO
!!------------------------------------------------------------------------------

Room	obj_DORMITORIO "Dormitorio"
 with	name_f	'habitacion',
		name_m	'dormitorio',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		n_to	obj_BANNO,
		w_to	obj_PASILLO,
 has	male;


!!==============================================================================
!!	BAÑO
!!------------------------------------------------------------------------------

Room	obj_BANNO "Baño"
 with	name_m	'bano',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		s_to	obj_DORMITORIO,
		sw_to	obj_PASILLO,
 has	male;


