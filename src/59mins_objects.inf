

!!==============================================================================
!!
!!	59 MINUTOS
!!	Definición de localidades y objetos de la casa
!!
!!==============================================================================
!!
!!	File:			59mins_objects.inf
!!	Author(s):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Languague:		ES (Castellano)
!!	System:			Inform-INFSP 6
!!	Platform:		Máquina-Z / GLULX
!!	Version:		0.1
!!	Released:		2014/03/05
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de 59 MINUTOS.
!!
!!	59 MINUTOS es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General GNU
!!	publicada por la Fundación para el Software Libre, ya sea la versión
!!	3 de la Licencia, o (a su elección) cualquier versión posterior.
!!
!!	59 MINUTOS se distribuye con la esperanza de que sea útil, pero
!!	SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita MERCANTIL o
!!	de APTITUD PARA UN PROPÓSITO DETERMINADO. Consulte los detalles de
!!	la Licencia Pública General GNU para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública General GNU
!!	junto a 59 MINUTOS. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2014, J. Francisco Martín
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	LIMBO: En algún lugar
!!------------------------------------------------------------------------------

Room	obj_LIMBO "En algún lugar"
 with	name	'limbo' 'lugar' 'sueno' 'vacio',
		description [; return O__M(self, 1); ],
		clarification 0;

!!	 *	FIXME - adjectives
Atrezzo	"paredes" 
 with	found_in
			obj_BANNO obj_COCINA obj_DORMITORIO obj_PASILLO obj_RECIBIDOR 
			obj_SALON obj_TALLER obj_TERRAZA,
		name_m	'muro',
		name_mp	'muros',
		name_f	'pared' 'esquina',
		name_fp	'paredes' 'esquinas',
		gender	G_FEMENINO + G_PLURAL,
		adjectives
			'amarillenta' 'amarillentas' 'amarillento' 'amarillentos' 'blanca' 
			'blancas' 'blanco' 'blancos',
		before [;
			Examine, Search:
				switch (GRAMMATICAL_INFLECTION) {
					1,2,3: "No hay nada especial en ", (the) self, ".";
					4,5,6: "No había nada especial en ", (the) self, ".";
				}
			default:
				switch (GRAMMATICAL_INFLECTION) {
					1: "No creo que sea necesario preocuparme por ";
					2: "No es necesario que te preocupes por ";
					3: "No es necesario que se preocupe por ";
					4: "No creí que fuese necesario preocuparme por ";
					5: "No era necesario que te preocupases por ";
					6: "No era necesario que se preocupase por ";
				}
				SpanishNominativePronoun(self);
				".";
		], 
 has	female pluralname;

NPC		obj_Silvia "Silvia" obj_LIMBO
 with	name_f	'silvia',
		name_m	'personaje',
		gender	G_FEMENINO,
		description [; return L__M(##Miscellany, 19); ],
 has	female proper;

!!	 *	FIXME - nombres
!!	 *	FIXME - adjectives
!!	 *	FIXME - description
!!	 *	FIXME - rutinas before y after
Item	obj_ropa "ropa" obj_Silvia
 class	Receptacle
 with	name_f	'ropa',
		gender	G_FEMENINO,
		article	 "su",
		adjectives 'brown' 'olive' 'green',
		description [;
			"A tasteful ensemble from Ann Taylor in muted browns and olive 
			greens (she has been an automn person since she was old enough to 
			match her own socks).";
		],
		react_before [;
			DisrobeX:
				if (noun == nothing)
					<<Disrobe self>>;
			WearX:
				if (noun == nothing)
					<<Wear self>>;
		],
		before [;
		],
		after [;
		],
		capacity_size 3,
		size 4,
 has	clothing container female open worn;


!!==============================================================================
!!	COCINA
!!------------------------------------------------------------------------------

Room	obj_COCINA "Cocina"
 with	name	'cocina',
		description
"Grande y espaciosa, como el resto de habitaciones de la casa, con ventanas a la playa. Tiene una amplia encimera en la que hay un aparato radiocasete y una cafetera italiana. Hay también una nevera (con algo de comida dentro), y un mechero por alguna parte. Tiene salidas al salón y a una terraza trasera, con acceso directo a la playa.",
		 e_to	obj_SALON,
		 n_to	obj_TERRAZA;


!!==============================================================================
!!	TERRAZA
!!------------------------------------------------------------------------------

Room	obj_TERRAZA "Terraza"
 with	name	'terraza' 'patio',
		description
"Una terraza trasera con acceso a la casa, a un pequeño taller (una construcción anexa a la casa para guardar trastos y herramientas) y a la playa (a través de una pequeña pasarela cerrada con una portilla). Hay una mesa y algunas sillas de exterior, deterioradas ya por el tiempo y el salitre, un aro de baloncesto sin red colgado a la fachada de la casa y un caballete para lienzos a un lado (de su padre).",
		 n_to	obj_PLAYA,
		 s_to	obj_COCINA,
		 w_to	obj_TALLER;

Atrezzo	"aro de baloncesto" obj_TERRAZA
 with	name_f	'canasta',
		name_m	'aro',
		gender	G_MASCULINO,
		adjectives 'baloncesto',
		before [;
			Use:
				if (~~IndirectlyContains(player, obj_pelotaBaloncesto))
					"No puede jugar sin un balón.";
				print "Se coloca frente a la canasta y se prepara para 
				lanzar un tiro libre...^";
				switch (self.free_throw()) {
				0:	"^¡Encesta!";
				1:	"^La pelota da en el aro y sale rebotada sin entrar.";
				default: "^Falla el tiro.";
				}
		],
 private
		free_throw [ x;
			x = AimAndFire();
			if (x == -2 || x == -1) return random(0,1,2,3,4);
			if (x < 3) return 0;
			if (x < 8) return 1;
			return 2;
		],
 has	male;


!!==============================================================================
!!	TALLER
!!------------------------------------------------------------------------------

Room	obj_TALLER "Pequeño taller"
 with	name	'taller',
		description
"Poco más que un trastero lleno de herramientas. Todo lo que hay aquí dentro eran cosas de su padre, y está prácticamente tal y como lo dejó él antes de morir: se puede aprovechar para hablar de él y de la relación que tenían. Hay pinturas y disolventes y algunos lienzos (al padre le gustaba pintar). Si el jugador rebusca entre los trastos, puede encontrar también un rifle de caza escondido. Nunca hubiese imaginado que su padre pudiese tener un arma.",
		e_to	obj_TERRAZA;


!!==============================================================================
!!	PLAYA
!!------------------------------------------------------------------------------

Room	obj_PLAYA "Playa"
 with	name	'playa',
		description
"Una playa completamente desierta. Para bajar desde la terraza se quita antes las zapatillas. Nunca le ha gustado ir a la playa en la ciudad, saturada siempre de gente tostándose al sol. Pero esta si. Es agradable sentir la arena entre los pies y el penetrante olor del mar.",
		s_to	obj_TERRAZA;


!!==============================================================================
!!	SALON
!!------------------------------------------------------------------------------

Room	obj_SALON "Salón"
 with	name	'salon' 'sala' 'comedor',
		description
"La habitación más grande de la casa. Es un lugar agradable, con un par de estanterías repletas de libros, un par de sofás y varios sillones, un mueble bar ---dentro del que hay solamente una botella de güisqui casi vacía y un puñado de trofeos de certámenes de fotografía y pintura locales, cosa de su padre--- y un antiguo escritorio de madera oscura. Hay también un televisor con reproductor de DVD y, sobre la mesa, su bolso, en el que hay un montón de exámenes sin corregir de sus alumnos de primero de bachillerato de literatura. Está conectada con la cocina y el recibidor.",
		s_to	obj_RECIBIDOR,
		w_to	obj_COCINA;


!!==============================================================================
!!	RECIBIDOR
!!------------------------------------------------------------------------------

Room	obj_RECIBIDOR "Recibidor"
 with	name	'recibidor',
		description
"El mueble más destacado es una cómoda junto a la puerta principal de la casa. Sobre ella hay un teléfono, y dentro de uno de los cajones se puede encontrar una cajetilla de tabaco, de su padre. Desde el recibidor se puede ir al salón o subir por las escaleras hacia el segundo piso.",
		n_to	obj_SALON,
		u_to	obj_PASILLO;


!!==============================================================================
!!	PASILLO
!!------------------------------------------------------------------------------

Room	obj_PASILLO "Pasillo de la segunda planta"
 with	name	'pasillo',
		description
"Conecta el piso de abajo a través de las escaleras con una habitación multiusos al norte, el dormitorio principal al este, y un baño al noreste. Colgadas en la pared hay viejas fotos enmarcadas, algunas de cuando era niña, con sus padres.",
		d_to	obj_RECIBIDOR,
		e_to	obj_DORMITORIO,
		n_to	obj_HABITACION,
		ne_to	obj_BANNO;


!!==============================================================================
!!	HABITACION
!!------------------------------------------------------------------------------

Room	obj_HABITACION "Habitación"
 with	name	'habitacion',
		description
"Su viejo dormitorio. Cuando dejó de vivir aquí, su padre lo cambió. Ahora hay una bicicleta estática en vez de una cama, un gran escritorio y un armario. Tiene una puerta al pasillo al sur.",
		s_to	obj_PASILLO;

Item	obj_pelotaBaloncesto "pelota de baloncesto" obj_HABITACION
 with	name_f	'pelota',
		name_m	'balon',
		gender	G_FEMENINO,
		adjectives 'baloncesto',
 has	female;


!!==============================================================================
!!	DORMITORIO
!!------------------------------------------------------------------------------

Room	obj_DORMITORIO "Dormitorio"
 with	name	'dormitorio' 'habitacion',
		description
"Es el dormitorio de su padre. Estuvo recogiendo y limpiando el año anterior, unos días después de que muriese, pero no lo ha tocado mucho desde entonces. Tiene una gran cama, un armario y un reloj despertador. Comunica con el pasillo al oeste, y con un baño al norte.",
		n_to	obj_BANNO,
		w_to	obj_PASILLO;


!!==============================================================================
!!	BAÑO
!!------------------------------------------------------------------------------

Room	obj_BANNO "Baño"
 with	name	'baño',
		description
"Es un pequeño baño con puertas al dormitorio y al pasillo. Tiene una bañera lo suficientemente grande como para entrar tumbado dentro, un espejo en el que Silvia podrá mirarse, y un armarito con algún producto de higiene y medicinas de su padre. Entre las medicinas hay una caja de Lexatin (vacía) y varias cajas de Diazepam.",
		s_to	obj_DORMITORIO,
		sw_to	obj_PASILLO;


